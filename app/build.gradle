apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

// https://docs.deploygate.com/docs/gradle-plugin
apply plugin: 'deploygate'

// https://github.com/ben-manes/gradle-versions-plugin
apply plugin: 'com.github.ben-manes.versions'

// https://firebase.google.com/docs/crashlytics/get-started?platform=android
apply plugin: 'com.google.firebase.crashlytics'

// https://firebase.google.com/docs/perf-mon/get-started-android
apply plugin: 'com.google.firebase.firebase-perf'

// https://github.com/jlleitschuh/ktlint-gradle
apply plugin: "org.jlleitschuh.gradle.ktlint"

android {
    compileSdkVersion 30

    buildFeatures {
        dataBinding true
    }
    defaultConfig {
        applicationId "hm.orz.chaos114.android.tumekyouen"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 25
        versionName "0.12.0"
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    signingConfigs {
        debug {
            storeFile file("cert/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            storeFile file("cert/release.keystore")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias "chaos114"
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false
        lintConfig file('lint.xml')
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

ext {
    coroutinesVersion = '1.4.2'
    okhttpLibVersion = '4.9.0'
    retrofitLibVersion = '2.9.0'
    moshiLibVersion = '1.11.0'
    icepickVersion = '3.2.0'
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutinesVersion}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${coroutinesVersion}"
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'androidx.appcompat:appcompat:1.2.0'

    implementation 'com.google.firebase:firebase-core:18.0.0'
    implementation 'com.google.firebase:firebase-auth:20.0.1'
    implementation 'com.google.firebase:firebase-messaging:21.0.0'
    implementation 'com.google.firebase:firebase-ads:19.6.0'
    implementation 'com.google.firebase:firebase-perf:19.0.10'

    // https://developer.android.com/jetpack/androidx/releases/lifecycle (ViewModel and LiveData)
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version"

    implementation 'com.google.firebase:firebase-analytics:18.0.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.3.0'

    // room
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    androidTestImplementation "android.arch.persistence.room:testing"

    // Twitter SDK
    implementation 'com.twitter.sdk.android:twitter-core:3.3.0'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation "com.squareup.okhttp3:okhttp:${okhttpLibVersion}"
    implementation "com.squareup.okhttp3:logging-interceptor:${okhttpLibVersion}"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:${okhttpLibVersion}"
    implementation "com.squareup.retrofit2:retrofit:${retrofitLibVersion}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofitLibVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofitLibVersion}"
    implementation "com.squareup.retrofit2:converter-moshi:${retrofitLibVersion}"
    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.uber.autodispose:autodispose-ktx:1.2.0'
    implementation 'com.uber.autodispose:autodispose-android-ktx:1.2.0'
    implementation 'com.uber.autodispose:autodispose-android-archcomponents-ktx:1.2.0'
    implementation "com.squareup.moshi:moshi:${moshiLibVersion}"
    implementation "com.squareup.moshi:moshi-kotlin:${moshiLibVersion}"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:${moshiLibVersion}"

    // Icepick
    implementation "frankiesardo:icepick:${icepickVersion}"
    kapt "frankiesardo:icepick-processor:${icepickVersion}"

    // Dagger2
    def dagger_version = "2.30.1"
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    testImplementation 'junit:junit:4.13.1'
    testImplementation "org.robolectric:robolectric:4.4"
    testImplementation "org.robolectric:shadows-multidex:4.4"
}

ktlint {
    version = "0.40.0"
    android = true
    reporters {
        reporter "checkstyle"
    }
    ignoreFailures = true
    filter {
        exclude("**/generated/**")
        exclude("**/network/models/**")
        include("**/java/**")
    }
}

detekt {
    version = "1.0.0-RC16"
    input = files("src/main/java")
    config = files("$rootDir/detekt.yml")
}
tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
    exclude("**/network/models/**")
}

apply plugin: 'com.google.gms.google-services'
