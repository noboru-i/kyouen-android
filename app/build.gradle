apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'deploygate'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    useLibrary 'org.apache.http.legacy'

    dataBinding {
        enabled = true
    }
    defaultConfig {
        applicationId "hm.orz.chaos114.android.tumekyouen"
        minSdkVersion 17
        targetSdkVersion 23
        versionCode 19
        versionName "0.10.1"
    }
    signingConfigs {
        debug {
            storeFile file("cert/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            storeFile file("cert/release.keystore")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias "chaos114"
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false
        lintConfig file('lint.xml')
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

ext {
    supportLibVersion = '23.4.0'
    firebaseLibVersion = '9.2.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"

    compile "com.google.firebase:firebase-core:${firebaseLibVersion}"
    compile "com.google.firebase:firebase-analytics:${firebaseLibVersion}"
    compile "com.google.firebase:firebase-messaging:${firebaseLibVersion}"
    compile "com.google.firebase:firebase-ads:${firebaseLibVersion}"
    compile "com.google.firebase:firebase-crash:${firebaseLibVersion}"

    compile 'org.projectlombok:lombok:1.16.6'
    compile('com.twitter.sdk.android:twitter:1.13.1@aar') {
        transitive = true;
    }
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.squareup.okhttp3:okhttp:3.4.0-RC1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.0-RC1'
    compile "com.squareup.okhttp3:okhttp-urlconnection:3.4.0-RC1"
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'

    compile 'com.google.dagger:dagger:2.2'
    apt 'com.google.dagger:dagger-compiler:2.2'
    provided 'javax.annotation:jsr250-api:1.0'

    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.1"
    testCompile "org.robolectric:shadows-support-v4:3.1"
}

apply from: "https://raw.githubusercontent.com/monstar-lab/gradle-android-ci-check/1.3.0/ci.gradle"
apply from: '../tasks/jacoco.gradle'
apply plugin: 'com.google.gms.google-services'
