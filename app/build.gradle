apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

// https://firebase.google.com/docs/crashlytics/get-started
apply plugin: 'io.fabric'

// https://docs.deploygate.com/docs/gradle-plugin
apply plugin: 'deploygate'

// https://github.com/ben-manes/gradle-versions-plugin
apply plugin: 'com.github.ben-manes.versions'

// https://firebase.google.com/docs/perf-mon/get-started-android
apply plugin: 'com.google.firebase.firebase-perf'

// https://github.com/jlleitschuh/ktlint-gradle
apply plugin: "org.jlleitschuh.gradle.ktlint"

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    dataBinding {
        enabled = true
    }
    defaultConfig {
        applicationId "hm.orz.chaos114.android.tumekyouen"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 25
        versionName "0.12.0"
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    signingConfigs {
        debug {
            storeFile file("cert/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            storeFile file("cert/release.keystore")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias "chaos114"
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false
        lintConfig file('lint.xml')
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    okhttpLibVersion = '3.11.0'
    retrofitLibVersion = '2.4.0'
    icepickVersion = '3.2.0'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.browser:browser:1.0.0'

    implementation 'com.google.firebase:firebase-core:16.0.7'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    implementation 'com.google.firebase:firebase-ads:17.1.3'
    implementation 'com.google.firebase:firebase-perf:16.2.3'

    // https://developer.android.com/jetpack/androidx/releases/lifecycle (ViewModel and LiveData)
    def lifecycle_version = "2.0.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version"

    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'

    // room
    def room_version = "2.1.0-alpha04"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    androidTestImplementation "android.arch.persistence.room:testing"

    // Twitter SDK
    implementation 'com.twitter.sdk.android:twitter-core:3.3.0'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "com.squareup.okhttp3:okhttp:${okhttpLibVersion}"
    implementation "com.squareup.okhttp3:logging-interceptor:${okhttpLibVersion}"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:${okhttpLibVersion}"
    implementation "com.squareup.retrofit2:retrofit:${retrofitLibVersion}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofitLibVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofitLibVersion}"
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'com.uber.autodispose:autodispose-ktx:1.0.0'
    implementation 'com.uber.autodispose:autodispose-android-ktx:1.0.0'
    implementation 'com.uber.autodispose:autodispose-android-archcomponents-ktx:1.0.0'

    // Icepick
    implementation "frankiesardo:icepick:${icepickVersion}"
    kapt "frankiesardo:icepick-processor:${icepickVersion}"

    // Dagger2
    def dagger_version = "2.20"
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.0.1"
    testImplementation "org.robolectric:shadows-multidex:4.0.1"
}

ktlint {
    version = "0.30.0"
    android = true
    reporters = [org.jlleitschuh.gradle.ktlint.reporter.ReporterType.CHECKSTYLE]
    ignoreFailures = true
    filter {
        exclude("**/generated/**")
        include("**/java/**")
    }
}

detekt {
    version = "1.0.0-RC14"
    input = files("src/main/java")
    filters = ".*/resources/.*,.*/build/.*"
    config = files("$rootDir/detekt.yml")
}

apply plugin: 'com.google.gms.google-services'
